plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.12'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'maven-publish'
}

group = 'dev.observability'
version = '0.3.9'
description = 'Observability Starter for Github Packages'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

publishing {
	publications {
		maven(MavenPublication) {
			from components.java

			groupId = 'dev.observability'
			artifactId = 'observability-starter'
			version = '0.3.9'

			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}

			pom {
				name = 'Observability Spring Boot Starter'
				description = 'A reusable Spring Boot Starter for production-ready observability with Prometheus and Grafana'
				url = 'https://github.com/yukmekim/observability-starter'

				licenses {
					license {
						name = 'MIT License'
						url = 'https://opensource.org/licenses/MIT'
					}
				}

				developers {
					developer {
						id = 'yukmekim'
						name = 'YUK JANG HUN'
						email = 'gyjj1243@gmail.com'
					}
				}
			}
		}
	}
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/yukmekim/observability-starter")
			credentials {
				username = project.findProperty("gpr.user")
				password = project.findProperty("gpr.token")
			}
		}
	}
}

dependencies {
	// 모니터링 핵심 의존성 (추가)
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'
    
    // Logging & Loki
    implementation 'com.github.loki4j:loki-logback-appender:1.6.0'
    implementation 'net.logstash.logback:logstash-logback-encoder:8.0'

    implementation 'org.springframework.boot:spring-boot-starter-web'

    // Auto Configuration 지원
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('bootJar') {
	enabled = false;
}

tasks.named('jar') {
	enabled = true;
}
